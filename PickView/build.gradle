apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.1"

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile 'com.android.support:appcompat-v7:24.1.1'
}

// 以下配置为发布到jcenter仓库：
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group GROUP_ID
version = PICK_VIEW_VERSION

// 项目的主页
def siteUrl = 'https://github.com/ouyangzn/Android-Library/tree/master/PickViewLibrary'
// Git仓库的url --> https://github.com/ouyangzn/Android-Library.git
def gitUrl = 'git@github.com:ouyangzn/Android-Library.git'

install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        groupId GROUP_ID
        artifactId PICK_VIEW_ARTIFACT_ID
        version = PICK_VIEW_VERSION
        // Add your description here 项目的描述
        name PICK_VIEW_DESCRIPTION
        url siteUrl
        // Set your license
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            //填写的一些基本信息
            id USER
            name 'Z、ero'
            email 'ouyangzhaoneng@gmail.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

// build a jar with source files
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  failOnError false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
javadoc {
  options {
    encoding "UTF-8"
    charSet 'UTF-8'
    author true
    version PICK_VIEW_VERSION
    links "http://docs.oracle.com/javase/7/docs/api"
    title PICK_VIEW_ARTIFACT_ID
  }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    // 在bintray上的repository仓库名
    repo = "Android-lib"
    // project name in jcenter工程名
    name = PICK_VIEW_ARTIFACT_ID
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["Apache-2.0"]
    publish = true
    version {
      name = PICK_VIEW_VERSION
      desc = PICK_VIEW_DESCRIPTION
    }
  }
}
